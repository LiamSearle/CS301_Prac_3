
   1   COMPILER Parva $CN
   2   /* Parva level 1 grammar  - Coco/R for C# (EBNF)
   3      P.D. Terry, Rhodes University, 2003
   4      Grammar only */
   5 
   6   CHARACTERS
   7     lf         = CHR(10) .
   8     backslash  = CHR(92) .
   9     control    = CHR(0) .. CHR(31) .
  10     letter     = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
  11                  + "abcdefghijklmnopqrstuvwxyz" .
  12     digit      = "0123456789" .
  13     stringCh   = ANY - '"' - control - backslash .
  14     charCh     = ANY - "'" - control - backslash .
  15     printable  = ANY - control .
  16 
  17   TOKENS
  18     identifier = letter { letter | digit | "_" } .
  19     number     = digit { digit } .
  20     stringLit  = '"' { stringCh | backslash printable } '"' .
  21     charLit    = "'" ( charCh   | backslash printable ) "'" .
  22 
  23   COMMENTS FROM "//" TO lf
  24   COMMENTS FROM "/*" TO "*/"
  25   IGNORE CHR(9) .. CHR(13)
  26 
  27   PRODUCTIONS
  28     Parva             = "void" identifier "(" ")" Block .
  29     Block             = "{" { Statement } "}" .
  30     Statement         =   Block | ";"
  31                         | ConstDeclarations | VarDeclarations
  32                         | Assignment | IfStatement | WhileStatement
  33                         | ReturnStatement | HaltStatement
  34                         | ReadStatement | WriteStatement .
  35     ConstDeclarations = "const" OneConst { "," OneConst } ";" .
  36     OneConst          = identifier "=" Constant .
  37     Constant          =   number | charLit
  38                         | "true" | "false" | "null" .
  39     VarDeclarations   = Type OneVar { "," OneVar } ";" .
  40     OneVar            = identifier [ "=" Expression ] .
  41     Assignment        = Designator "=" Expression ";" .
  42     Designator        = identifier [ "[" Expression "]" ] .
  43     IfStatement       = "if" "(" Condition ")" Statement .
  44     WhileStatement    = "while" "(" Condition ")" Statement .
  45     ReturnStatement   = "return" ";" .
  46     HaltStatement     = "halt" ";" .
  47     ReadStatement     = "read" "(" ReadElement
  48                                    { "," ReadElement } ")" ";" .
  49     ReadElement       = stringLit | Designator .
  50     WriteStatement    = "write" "(" WriteElement
  51                                     { "," WriteElement } ")" ";" .
  52     WriteElement      = stringLit | Expression .
  53     Condition         = Expression .
  54     Expression        = AddExp [ RelOp AddExp ] .
  55     AddExp            = [ "+" | "-" ] Term { AddOp Term } .
  56     Term              = Factor { MulOp Factor } .
  57     Factor            =   Designator | Constant
  58                         | "new" BasicType "[" Expression "]"
  59                         | "!" Factor | "(" Expression ")" .
  60     Type              = BasicType [ "[]" ] .
  61     BasicType         = "int" | "bool" .
  62     AddOp             = "+" | "-" | "||" .
  63     MulOp             = "*" | "/" | "&&" | "%" .
  64     RelOp             = "==" | "!=" | "<" | "<=" | ">" | ">=" .
  65   END Parva.

0 errors detected
